# -*- coding: utf-8 -*-

# Bogus Balangao

################################## DATA #################################

# Simplify for syllabification
define ROOTS {hIgIp} | {hIdIm} | {hIdIn} | {gihib} ;

############################# BASIC DEFINITIONS #########################

define V [ a | %[aPrimaryStress%] | %[aSecondaryStress%] | 
    e | %[ePrimaryStress%] | %[eSecondaryStress%] |
    i | %[iPrimaryStress%] | %[iSecondaryStress%] |
    I | %[IPrimaryStress%] | %[ISecondaryStress%] |
    o | %[oPrimaryStress%] | %[oSecondaryStress%] |
    u | %[uPrimaryStress%] | %[uSecondaryStress%] ] ;


# q = glottal stop
define C [ b | d | g | h | k | m | n | p | q | t ] ;

define SEGMENT [ V | C ] ;

define HighV [ u | i | I ] ;
define GLOTTAL [ h | q ] ;
# sholud call this "non-glottal obstruent"
define OBSTRUENT [ b | d | g | k | p | t] ;
define SONORANT [ V ] ;
define STRESSED [ %[aPrimaryStress%] | %[ePrimaryStress%] | %[iPrimaryStress%] |
%[IPrimaryStress%] | %[oPrimaryStress%] | %[uPrimaryStress%] ] |
    [ %[aSecondaryStress%] | %[eSecondaryStress%] | %[iSecondaryStress%] | 
    %[ISecondaryStress%] | %[oSecondaryStress%] | %[uSecondaryStress%] ] ;

define PRIMARYSTRESS [ %[aPrimaryStress%] | %[ePrimaryStress%] | 
    %[iPrimaryStress%] | %[IPrimaryStress%] | %[oPrimaryStress%] | 
    %[uPrimaryStress%] ] ;

define SECONDARYSTRESS [ %[aSecondaryStress%] | %[eSecondaryStress%] | 
    %[iSecondaryStress%] | %[ISecondaryStress%] | %[oSecondaryStress%] | 
    %[uSecondaryStress%] ] ;

define GENTAGS [ %+DelV | %+Met ] ;

#define TAGS [ PERSON | NUMBER | TENSE ] ;

#define BOUNDARY [ %+ | .#. | . ] ;
define BOUNDARY [ %+ | .#. | %. ] ;
define EDGE [ .#. | %. ] ;

# Syllable boundary
define SB [~$%. %. ~$%.];

define Light [C* V];
define Heavy [Light SEGMENT+];

define S [ Heavy | Light ] ;
define SS [ Heavy | Light .o. $[STRESSED]] ;
define USS [ Heavy | Light .o. ~$[STRESSED]] ;

############################# MORPHOLOGY ################################

# To do: allow free order in tags
# Do metathesis first
# All persons and numbers except 3Sing
# Tags contain 1st, or 2nd, or 3rd and Pl

define PREFIXES [ {qi} | {ma} | {pu} ] %+ ;
define SUFFIXES %+ [ {In} ] ;

define Morphology (PREFIXES) ROOTS | ROOTS (SUFFIXES);
#define Morphology ROOTS ;

################################### GEN ##################################

define Syllabify SEGMENT (->) ... "." || _ SEGMENT/[GENTAGS | %+] ;

# Had Shuffle rules in this form, but - while it worked - it was 
# very inefficient and led to memory problems
#[ [?* m:l l:m ?* []:%+Met]/GENTAGS | ~$[m l]/GENTAGS ] | ?* ].o.

define Shuffle h q (->) q h %+Met, q h (->) h q %+Met,
h b (->) b h %+Met, h d (->) d h %+Met, h g (->) g h %+Met, 
g h (->) h g %+Met, d h (->) h d %+Met, b h (->) h b %+Met, 
h t (->) t h %+Met, t h (->) h t %+Met,
h k (->) k h %+Met, k h (->) h k %+Met,
h p (->) p h %+Met, p h (->) h p %+Met,
q b (->) b q %+Met, q d (->) d q %+Met, q g (->) g q %+Met, 
g q (->) q g %+Met, d q (->) q d %+Met, b q (->) q b %+Met,
q t (->) t q %+Met, t q (->) q t %+Met,
q k (->) k q %+Met, k q (->) q k %+Met,
q p (->) p q %+Met, p q (->) q p %+Met,
b d (->) d b %+Met, b g (->) g b %+Met, 
g b (->) b g %+Met, d b (->) b d %+Met, 
b t (->) t b %+Met, t b (->) b t %+Met,
b k (->) k b %+Met, k b (->) b k %+Met,
d g (->) g d %+Met, g d (->) d g %+Met, 
d t (->) t d %+Met, t d (->) d t %+Met,
d k (->) k d %+Met, k d (->) d k %+Met,
g t (->) t g %+Met, t g (->) g t %+Met,
g k (->) k g %+Met, k g (->) g k %+Met,
k t (->) t k %+Met, t k (->) k t %+Met
#t %+DelV h (->) h %+DelV t %+Met
#define Shuffle h g (->) g h %+Met,
#h %+DelV g (->) g %+DelV h %+Met,
#define Shuffle h d (->) d h %+Met,
#h %+DelV d (->) d %+DelV h %+Met
;

# Let's start moving these
define DeletionVowel V (->) %+DelV .o. 
# Need to let through things withot %+Delv
0:%+DelV ?* %+DelV:0 ?* | ~[%+DelV] .o.  0:%+DelV ?* %+DelV:0 ?* | ~[%+DelV] .o.
0:%+DelV ?* %+DelV:0 ?* | ~[%+DelV] .o.  0:%+DelV ?* %+DelV:0 ?* | ~[%+DelV] .o.
0:%+DelV ?* %+DelV:0 ?* | ~[%+DelV] .o.  0:%+DelV ?* %+DelV:0 ?* | ~[%+DelV] .o.
0:%+DelV ?* %+DelV:0 ?* | ~[%+DelV] .o.  0:%+DelV ?* %+DelV:0 ?* | ~[%+DelV] 
;

define GenPrimaryStress a (->) %[aPrimaryStress%], e (->) %[ePrimaryStress%], 
    i (->)  %[iPrimaryStress%], I (->) %[IPrimaryStress%],
    o (->) %[oPrimaryStress%], u (->) %[uPrimaryStress%] ;

define GenSecondaryStress a (->) %[aSecondaryStress%], 
    e (->) %[eSecondaryStress%], i (->)  %[iSecondaryStress%], 
    I (->) %[ISecondaryStress%], o (->) %[oSecondaryStress%], 
    u (->) %[uSecondaryStress%] ;

define GenStress GenPrimaryStress .o. GenSecondaryStress ;

#define Gen [ Shuffle .o. DeletionVowel .o. Syllabify ] ;

# Add primary stress to all syllables
#
# Then add a constraint that limits primary stress to one per word
# And another constraint that limits primary stress to first syllable
define Gen [ DeletionVowel .o. GenStress .o. Shuffle .o. Syllabify ] ;
#define Gen [ DeletionVowel .o. Shuffle ] ;

######################### OT CONSTRAINTS #############################

# We use asterisks to mark constraint violations. Ordinary constraints
# such as Lapse assign single asterisks as the violation marks and the
# candidate with the fewest number is selected. Gradient constraints
# such as AllFeetFirst mark violations with sequences of asterisks.
# The number increases with distance from the word edge.

# Every instance of * in an output candidate is a violation.

define Viol ${*};

# We prune candidates with "lenient composition" that eliminates
# candidates that violate the constraint provided that at least
# one output candidate survives.

define Viol0 ~Viol;        # No violations
define Viol1 ~[Viol^2];    # At most one violation
define Viol2 ~[Viol^3];    # At most two violations
define Viol3 ~[Viol^4];    # etc.
define Viol4 ~[Viol^5];
define Viol5 ~[Viol^6];
define Viol6 ~[Viol^7];
define Viol7 ~[Viol^8];
define Viol8 ~[Viol^9];
define Viol9 ~[Viol^10];
define Viol10 ~[Viol^11];
define Viol11 ~[Viol^12];
define Viol12 ~[Viol^13];
define Viol13 ~[Viol^14];
define Viol14 ~[Viol^15];
define Viol15 ~[Viol^16];

# This eliminates the violation marks after the candidate set has
# been pruned by a constraint.

define Pardon {*} -> 0;

########################## CONSTRAINTS ##############################

# Faithfulness constraints

define Linearity [ %+Met -> ... {*}] ;

define IdentVoice [ %+DeltaVoice -> ... {*}] ;

define IdentNasal [ %+DeltaNasal -> ... {*}] ;

define MaxV [ %+DelV -> ... {*}] ;

# Markedness constraints

define NoGlottObs [ [ GLOTTAL OBSTRUENT ]/[GENTAGS | %+ | "."]-> ... {*}] ;

define NoComplexOnset [C C]/[GENTAGS | %+] -> ... {*} || EDGE _ ;

define NoComplexCoda [C C]/[GENTAGS | %+ ] -> ... {*} || _ EDGE ;

# If a syllable is all consonants, it violates this
# If we use ~$[V], are we okay?
# That might be a problem because it'll match syllable boundary
# but it's minimal matching? not greedy?
define SonorantNucleus ~$V -> ... {*} || EDGE _ EDGE ;

define HasOnset V -> ... {*} || EDGE/[GENTAGS | %+ ] _ ;

define HasPrimaryStress ~$PRIMARYSTRESS -> ... {*} || .#. _ .#. ;

define OnlyOnePrimaryStress $[PRIMARYSTRESS ?* PRIMARYSTRESS] -> ... {*} || .#. _ .#. ;

define NoStressClash SS/[GENTAGS | %+] "." SS/[GENTAGS | %+ ] -> ... {*} ;

# I might prefer to do this as binary feet
define NoDoubledUnstressed USS/[GENTAGS | %+] "." USS/[GENTAGS | %+ ] -> ... {*} ;

# Is this why syllabification is breaking?
define SSP [SEGMENT* V+ C+ V+ SEGMENT*]/[GENTAGS | %+] -> ... {*} || EDGE _ EDGE;

define InitialStress ~$PRIMARYSTRESS -> ... {*} || .#. _ EDGE/[GENTAGS | %+] ;

# No unstressed high vowels between two stressed syllables
define NoUnstressedHighVowel C*/[GENTAGS | %+] [ u | i | I ] C*/[GENTAGS | %+] 
    -> ... {*} || SS/[GENTAGS | %+] "." _ "." SS/[GENTAGS | %+ ];

########################## READABILITY ##############################

# Remove tags, turn placeholders to IPA
# ' is currently glottal stop
define Pretty [ GENTAGS | %+ ] -> 0 .o.
I -> É¨;

define Readable [GENTAGS | %+ ] -> 0 ;

# Letting through plenty of complex onsets when it doesn't have to
# TODO/FIXME 
# Shuffle has now broken syllabification
read regex Morphology .o. Gen 
.o. SonorantNucleus .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 
.o. HasOnset .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 
.o. SSP .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 
.o. NoComplexOnset .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 
.o. NoComplexCoda .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 
.o. HasPrimaryStress .O. Viol1 .O. Viol0 
.o. OnlyOnePrimaryStress .O. Viol1 .O. Viol0 
.o. NoStressClash .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 .o. Pardon
.o. NoDoubledUnstressed .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 .o. Pardon
.o. InitialStress .O. Viol1 .O. Viol0 .o. Pardon
.o. NoUnstressedHighVowel .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 .o. Pardon
#.o. MaxV .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 
# With pardon
.o. MaxV .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 .o. Pardon
.o. NoGlottObs .O. Viol5 .O. Viol4 .O. Viol3 .O. Viol2 .O. Viol1 .O. Viol0 .o. Pardon
#.o. Pretty
.o. Readable
;

# Need to get Linearity in there sometime

echo ** hIgIp
down hIgIp;
echo ** hIgIpIn
down hIgIp+In;
echo ** Should metathesize - expect pughip
echo ** puhIgIp
down pu+hIgIp;

echo ** hIdIm
down hIdIm;
echo ** hIdIm+In
down hIdIm+In;
echo ** Should metathesize - expect pudhim
echo ** pu+hIdIm
down pu+hIdIm;

echo ** hIdIn
down hIdIn;
echo ** hIdIn+In
down hIdIn+In;
echo ** Should metathesize - expect puhdin
echo ** pu+hIdIn
down pu+hIdIn;

echo ** gihib
down gihib;
echo ** Should metathesize - expect gibhin
echo ** gihib+In
down gihib+In;
echo ** pu+gihib
down pu+gihib;

# cleanup net ?
